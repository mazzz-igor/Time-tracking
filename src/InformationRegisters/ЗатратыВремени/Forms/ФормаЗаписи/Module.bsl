&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	МАКСИМУМ(ЗатратыВремениСрезПоследних.ВремяОкончание) КАК ВремяОкончание
			|ИЗ
			|	РегистрСведений.ЗатратыВремени.СрезПоследних(&ТекущаяДата, ) КАК ЗатратыВремениСрезПоследних";
		
		Запрос.УстановитьПараметр("ТекущаяДата", КонецДня(ТекущаяДата()));
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Запись.ВремяНачло = ВыборкаДетальныеЗаписи.ВремяОкончание;
		КонецЦикла;   
		
		Запись.ID = "";
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Округлить(Команда)
	
	Если ЭтаФорма.ТекущийЭлемент.Имя = "ОкруглитьВремяНачало15" Тогда
		Запись.ВремяНачло = НачалоЧаса(Запись.ВремяНачло) + (Цел(Минута(Запись.ВремяНачло)/15)*15)*60;
	ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ОкруглитьВремяНачало30" Тогда
		Запись.ВремяНачло = НачалоЧаса(Запись.ВремяНачло)+ (Цел(Минута(Запись.ВремяНачло)/30)*30)*60;
	ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ОкруглитьВремяНачало60" Тогда
		Запись.ВремяНачло = НачалоЧаса(Запись.ВремяНачло);
	ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ОкруглитьВремяОкончание15" Тогда
		Запись.ВремяОкончание = НачалоЧаса(Запись.ВремяОкончание) + ((Цел(Минута(Запись.ВремяОкончание)/15)+?(Минута(Запись.ВремяОкончание)/15=0,0,1))*15)*60;
	ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ОкруглитьВремяОкончание30" Тогда
		Запись.ВремяОкончание = НачалоЧаса(Запись.ВремяОкончание) + ((Цел(Минута(Запись.ВремяОкончание)/30)+?(Минута(Запись.ВремяОкончание)/30=0,0,1))*30)*60;
	ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ОкруглитьВремяОкончание60" Тогда
		Запись.ВремяОкончание = НачалоЧаса(Запись.ВремяОкончание)+ ?(Минута(Запись.ВремяОкончание)=0,0,3600);
	КонецЕсли;
	
	ОбновлениеКонвертацииВремени();
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВремяДляЛУ = Дата('00010101') + Запись.Секунд;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущееВремя(Команда)
	
	Если ЭтаФорма.ТекущийЭлемент.Имя = "ТекущееВремяНачало" Тогда
		
		Запись.ВремяНачло = ТекущаяДата();
		
	ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ТекущееВремяОкончание" Тогда
		
		Запись.ВремяОкончание = ТекущаяДата();
		
	КонецЕсли;
	
	ОбновлениеКонвертацииВремени();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеКонвертацииВремени()

	Запись.Период = ?(ЗначениеЗаполнено(Запись.ВремяНачло),Запись.ВремяНачло,ТекущаяДата());
	
	Запись.Секунд = Запись.ВремяОкончание - Запись.ВремяНачло;
	Запись.Минут =  Запись.Секунд / 60;
	Запись.Часов =  Запись.Секунд / 3600;
	
	ВремяДляЛУ = Дата('00010101') + Запись.Секунд;
	
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВремя(Элемент)
	
	ОбновлениеКонвертацииВремени();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВремя(Команда)
	
	ПараметрыФормыОткрытия = Новый Структура;
	
	Если ЭтаФорма.ТекущийЭлемент.Имя = "ВыбратьВремяНачало" Тогда
		
		ПараметрыФормыОткрытия.Вставить("ДатаВремя",Запись.ВремяНачло);
	
	ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ВыбратьВремяОкончание" Тогда
		
		ПараметрыФормыОткрытия.Вставить("ДатаВремя",Запись.ВремяОкончание);
	
	КонецЕсли;
	
	Оп = Новый ОписаниеОповещения("РезультатВыбораВремени", ЭтотОбъект, ПараметрыФормыОткрытия);
	ОткрытьФорму("РегистрСведений.ЗатратыВремени.Форма.ФормаВыбораВремени",ПараметрыФормыОткрытия,ЭтаФорма,,,,Оп,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатВыбораВремени(ДатаВремя,ПараметрыФормыОткрытия) Экспорт
	
	Если ДатаВремя <> Неопределено Тогда
		
		Если ЭтаФорма.ТекущийЭлемент.Имя = "ВыбратьВремяНачало" Тогда
			
			Запись.ВремяНачло = ДатаВремя;
			
		ИначеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "ВыбратьВремяОкончание" Тогда
			
			Запись.ВремяОкончание = ДатаВремя;
			
		КонецЕсли;
		
		ОбновлениеКонвертацииВремени();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаОрфографии(Команда)
// Создадим объект MS Word, он должен быть установлен 
	
	Попытка 
		Word = Новый COMОбъект("Word.Application"); 
	Исключение 
		ПоказатьПредупреждение(,"Microsoft Word не установлен!",,"Ошибка!"); 
		Возврат; 
	КонецПопытки; 
	
	НетОшибок = Word.CheckSpelling(Запись.Описание); 
	
	Если НетОшибок Тогда // Все правильно 
		Сообщить("Нет ошибок"); 
	Иначе // текст содержит ошибки. Проверим каждое слово и выведем ошибочные. 
		
		Док = Word.Documents.Add(); // Создадим новый документ 
		Область = Док.Range(0,0); // Получим пустую область в начале документа 
		Область.InsertBefore(Запись.Описание); // Добавим в документ текст 
		
		СписокОшибок.Очистить();
		
		Для каждого Слово Из Область.Words Цикл 
			
			СловоДляПроверки = СокрЛП(Слово.Text); 
			НетОшибок = Word.CheckSpelling(СловоДляПроверки); 
			
			Если НЕ НетОшибок Тогда // Слово ошибочно 
				// покажем возможные замены неправильного слова 
				СтрокаВариантов = ""; 
				// Получим варианты правописания 
				Варианты = Слово.GetSpellingSuggestions( ,1, ,0);
				
				ПервоеСлово = Истина;
				Для каждого Вариант Из Варианты Цикл 
					
					СтрокаВариантов = СтрокаВариантов + ", " + Вариант.Name; 
					
					Если ПервоеСлово Тогда
						стр = СписокОшибок.Добавить();
						стр.СловоСОшибкой = СловоДляПроверки;
						стр.ПервоеСловоЗамены = Вариант.Name;
						ПервоеСлово = Ложь;
					КонецЕсли; 
					
				КонецЦикла; 
				
				Если Не ПервоеСлово Тогда
					СтрокаВариантов = ". Варианты замены: " + Сред(СтрокаВариантов, 2); 
					Сообщить("Ошибка в слове: " + СловоДляПроверки + СтрокаВариантов); 
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла; 
		
		Док.Close(0,,); // закроем документ без сохранения wdDoNotSaveChanges 
	КонецЕсли; 
	
	Word.Quit(); // закроем Word 
	
КонецПроцедуры

&НаКлиенте
Процедура Исправить(Команда)
	
	Для каждого стр Из СписокОшибок Цикл
		
		Запись.Описание  = СтрЗаменить(Запись.Описание,стр.СловоСОшибкой,стр.ПервоеСловоЗамены);	
		Модифицированность = Истина;

	КонецЦикла; 
	
КонецПроцедуры

